plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

ext.moduleName = "tech.guiyom.anscapes"
group 'com.github.Gui-Yom'
version '0.9.0'
mainClassName = "tech.guiyom.anscapes.Main"

sourceCompatibility = 11
targetCompatibility = 11

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'anscapes'
                description = 'Color codes and images for your terminal'
                url = 'https://github.com/Gui-Yom/anscapes'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/Gui-Yom/anscapes/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'Gui-Yom'
                        name = 'Guillaume Anthouard'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Gui-Yom/anscapes.git'
                    developerConnection = 'scm:git:ssh://github.com/Gui-Yom/anscapes.git'
                    url = 'https://github.com/Gui-Yom/anscapes/'
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Automatic-Module-Name": moduleName
        attributes "Main-Class": mainClassName
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean.doFirst {
    delete "temp"
    delete "out"
}
